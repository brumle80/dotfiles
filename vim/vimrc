"
"   ██╗   ██╗ ██╗ ███╗   ███╗ ██████╗   ██████╗
"   ██║   ██║ ██║ ████╗ ████║ ██╔══██╗ ██╔════╝
"   ██║   ██║ ██║ ██╔████╔██║ ██████╔╝ ██║
"   ╚██╗ ██╔╝ ██║ ██║╚██╔╝██║ ██╔══██╗ ██║
" ██╗╚████╔╝  ██║ ██║ ╚═╝ ██║ ██║  ██║ ╚██████╗
" ╚═╝ ╚═══╝   ╚═╝ ╚═╝     ╚═╝ ╚═╝  ╚═╝  ╚═════╝
"
if has("vim_starting") | set encoding=utf-8 nobomb | endif
scriptencoding utf-8
" note: set encoding BEFORE scriptencoding

" weird terminal bug wher ^[[2;2R appears after filename
set t_u7=

let g:dko_rtp_original = &runtimepath

" ============================================================================
" Settings vars
" ============================================================================

" Used by anything that needs to change/use loc list
let g:dko_loc_list_height = 10

" ============================================================================
" My autocmd block - Clear vimrc group's autocmds if reloading
" ============================================================================

augroup vimrc
  autocmd!
augroup END

" ============================================================================
" My defaults
" May be overridden by **/plugins, after/plugins and **/ftplugins
" ============================================================================

set modeline

" ----------------------------------------------------------------------------
" OS Related
" ----------------------------------------------------------------------------

" ------------------------------------
" Clipboard -- use os clipboard
" ------------------------------------

set pastetoggle=<F12>

if empty($SSH_CONNECTION) && has('clipboard')
  set clipboard=unnamed  " Use clipboard register

  " Share X windows clipboard. NOT ON NEOVIM -- neovim automatically uses
  " system clipboard when xclip/xsel/pbcopy are available.
  if has('nvim') && !has('mac')
    set clipboard=unnamedplus
  elseif has('unnamedplus')
    set clipboard+=unnamedplus
  endif
endif

" ----------------------------------------------------------------------------
" Display
" ----------------------------------------------------------------------------

set title                             " wintitle = filename - vim

" this is already set by modern terminal
"set ttyfast

if !has('nvim')
  set lazyredraw                      " macros don't update display
endif

" no beeps or flashes
set visualbell t_vb=

set number
set numberwidth=5

" show context around current cursor position
set scrolloff=8
set sidescrolloff=16

set colorcolumn=80
set cursorline

set synmaxcol=512                     " don't syntax highlight long lines

" ------------------------------------
" Tab line
" ------------------------------------

set showtabline=0                     " start OFF, toggle =2 to show tabline

" ------------------------------------
" Status line
" ------------------------------------

set laststatus=2                      " always show all statuslines

" ------------------------------------
" Command line
" ------------------------------------

set noshowcmd                         " show incomplete commands (SLOW so off)
set noshowmode                        " don't show -- INSERT -- in cmdline

" ----------------------------------------------------------------------------
" Input
" ----------------------------------------------------------------------------

" ------------------------------------
" Mouse
" ------------------------------------

set mouse=a
if !has('nvim') | set ttymouse=xterm | endif

" ------------------------------------
" Typing key combos
" ------------------------------------

set notimeout
set ttimeout
set ttimeoutlen=10

" no longer using comma so I can reverse repeat fFtT searches
" no longer using \ so I can type escaped chars :/
" needs to be before bundle so plugins map correctly
let g:mapleader = "\<Space>"

" ----------------------------------------------------------------------------
" Wild and file globbing stuff in command mode
" ----------------------------------------------------------------------------

set browsedir=buffer                  " browse files in same dir as open file
set wildmenu                          " Enhanced command line completion.
set wildmode=list:longest,full        " Complete files using a menu AND list
set wildignorecase
" output, VCS
set wildignore+=.git,.hg,.svn
set wildignore+=*/vendor/gems/*,*/vendor/cache/*,*/.bundle/*,*/.sass-cache/*,*.gem
set wildignore+=*.aux,*.out,*.toc
set wildignore+=*.o,*.obj,*.exe,*.dll,*.manifest,*.rbc,*.class
set wildignore+=*.ai,*.bmp,*.gif,*.ico,*.jpg,*.jpeg,*.png,*.psd,*.webp
set wildignore+=*.avi,*.m4a,*.mp3,*.oga,*.ogg,*.wav,*.webm
set wildignore+=*.eot,*.otf,*.ttf,*.woff
set wildignore+=*.doc,*.pdf
set wildignore+=*.zip,*.tar.gz,*.tar.bz2,*.rar,*.tar.xz
set wildignore+=*.swp,.lock,.DS_Store,._*

" ----------------------------------------------------------------------------
" File saving
" ----------------------------------------------------------------------------

"set autoread                         " reload files if they were edited
                                      " elsewhere
set fileformats=unix,mac,dos
set fileformat=unix

" info file -- nvim uses shada in $XDG_DATA_HOME so that's cool.
if !has('nvim')
  execute 'set viminfo+=n' . expand(dko#vim_dir . '/.tmp/cache/.viminfo')
endif

" From https://github.com/swizzard/dotfiles/blob/master/.vimrc
" Don't keep .viminfo information for files in temporary directories or shared
" memory filesystems; this is because they're used as scratch spaces for tools
" like sudoedit(8) and pass(1) and hence could present a security problem
if !has('nvim') && has('viminfo')
  augroup dkoviminfo
    autocmd!
    silent! autocmd vimrc BufNewFile,BufReadPre
        \ /tmp/*,$TMPDIR/*,$TMP/*,$TEMP/*,*/shm/*
        \ setlocal viminfo=
  augroup END
endif

" swap - useless
set noswapfile
execute 'set directory=' . dko#vim_dir . '/.tmp/swap//'

" backups
set backupskip=/tmp/*
" Make Vim able to edit crontab files again.
set backupskip+=,/private/tmp/*"
if has('mac')
  augroup dkomac
    autocmd!
    autocmd BufEnter  /private/tmp/crontab.*  setlocal backupcopy=yes
  augroup END
endif
execute 'set backupdir=' . dko#vim_dir . '/.tmp/backup//'
set backup

" undo files
" double slash means create dir structure to mirror file's path
set undofile
set undolevels=1000
set undoreload=10000
execute 'set undodir=' . dko#vim_dir . '/.tmp/undo//'

" ----------------------------------------------------------------------------
" Spellcheck
" ----------------------------------------------------------------------------

" Add symlinked aspell from dotfiles as default spellfile
"execute 'set spellfile=' . glob(expand(dko#vim_dir . '/en.utf-8.add'))

" ----------------------------------------------------------------------------
" Completion -- see also plugins/completion.vim
" ----------------------------------------------------------------------------

set complete-=i                       " don't complete includes
set complete-=t                       " don't complete tags
set completeopt-=preview              " don't open scratch preview
set completeopt+=menu,menuone         " show PUM, even for one thing

" @see <https://github.com/Valloric/YouCompleteMe/blob/master/autoload/youcompleteme.vim#L343>
if has('patch-7.4.314')
  set shortmess+=c                    " Disable "Pattern not found" messages
endif

" ----------------------------------------------------------------------------
" Splits and buffers
" ----------------------------------------------------------------------------

set splitbelow
set splitright
set fillchars=vert:│                  " Vertical sep for splits (unicode bar)

set hidden                            " remember undo after quitting

" reveal already opened files from the quickfix window instead of opening new
" buffers
set switchbuf=useopen

set nostartofline                     " don't jump to col1 on switch buffer

" ----------------------------------------------------------------------------
" Code folding
" ----------------------------------------------------------------------------

set nofoldenable
set foldcolumn=2
set foldlevel=99                      " show all folds by default
set foldlevelstart=99                 " show all folds by default

" ----------------------------------------------------------------------------
" Trailing whitespace
" ----------------------------------------------------------------------------

set list
set listchars=
set listchars+=tab:→\ 
set listchars+=trail:·
set listchars+=extends:»              " show cut off when nowrap
set listchars+=precedes:«
set listchars+=nbsp:⣿

" maybe...
" if has('patch-7.4.785')
"   set fixendofline
" endif

" ----------------------------------------------------------------------------
" Diffing
" ----------------------------------------------------------------------------

" Note this is += since fillchars was defined in splits
set fillchars+=diff:⣿
set diffopt=vertical                  " Use in vertical diff mode
set diffopt+=filler                   " blank lines to keep sides aligned
set diffopt+=iwhite                   " Ignore whitespace changes

" ----------------------------------------------------------------------------
" Input auto-formatting (global defaults)
" Probably need to update these in after/ftplugin too since ftplugins will
" probably update it.
" ----------------------------------------------------------------------------

set formatoptions=
set formatoptions+=c                  " Auto-wrap comments using textwidth
set formatoptions+=r                  " Continue comments by default
set formatoptions-=o                  " do not continue comment using o or O
set formatoptions+=q                  " continue comments with gq
set formatoptions-=a                  " auto-gq on type in comments?
set formatoptions+=n                  " Recognize numbered lists
set formatoptions+=2                  " Use indent from 2nd line of a paragraph
set formatoptions-=l                  " break lines that are already long?
set formatoptions+=1                  " Break before 1-letter words
" Vim 7.4 only: no // comment when joining commented lines
if v:version >= 704 | set formatoptions+=j | endif

set nrformats-=octal                  " never use octal when <C-x> or <C-a>

" ----------------------------------------------------------------------------
" Whitespace
" ----------------------------------------------------------------------------

set nowrap
set nojoinspaces                      " J command doesn't add extra space

" ----------------------------------------------------------------------------
" Indenting - overridden by indent plugins
" ----------------------------------------------------------------------------

set autoindent                        " indent when creating newline

" for autoindent, use same spaces/tabs mix as previous line, even if
" tabs/spaces are mixed. Helps for docblock, where the block comments have a
" space after the indent to align asterisks
set copyindent

" Try not to change the indent structure on "<<" and ">>" commands. I.e. keep
" block comments aligned with space if there is a space there.
set preserveindent

" Smart detect when in braces and parens. Has annoying side effect that it
" won't indent lines beginning with '#'. Relying on syntax indentexpr instead.
set nosmartindent

" Global setting. I don't edit C-style code all the time so don't default to
" C-style indenting.
set nocindent

" ----------------------------------------------------------------------------
" Tabbing - overridden by editorconfig, after/ftplugin
" ----------------------------------------------------------------------------

set expandtab                         " default to spaces instead of tabs
set shiftwidth=2                      " softtabs are 2 spaces for expandtab

" Alignment tabs are two spaces, and never tabs. Negative means use same as
" shiftwidth (so the 2 actually doesn't matter).
set softtabstop=-2

" real tabs render width. Applicable to HTML, PHP, anything using real tabs.
" I.e., not applicable to JS.
set tabstop=2

" use multiple of shiftwidth when shifting indent levels.
" this is OFF so block comments don't get fudged when using ">>" and "<<"
set noshiftround

" When on, a <Tab> in front of a line inserts blanks according to
" 'shiftwidth'. 'tabstop' or 'softtabstop' is used in other places.
set smarttab

set backspace=indent,eol,start        " bs anything

" ----------------------------------------------------------------------------
" Match and search
" ----------------------------------------------------------------------------

set matchtime=1                       " tenths of a sec
set showmatch                         " briefly jump to matching paren?
set hlsearch
set incsearch
set wrapscan                          " Searches wrap around end of the file.
set ignorecase
set smartcase

" The Silver Searcher
if executable('ag')
  set grepprg=ag\ --nogroup\ --nocolor
endif

" ----------------------------------------------------------------------------
" Special highlights
" ----------------------------------------------------------------------------

" misspellings
match ErrorMsg 'targett'
match ErrorMsg 'plugn'

" Highlight VCS conflict markers
" @see {@link https://bitbucket.org/sjl/dotfiles/src/83aac563abc9d0116894ac61db2c63c9a05f72be/vim/vimrc?at=default&fileviewer=file-view-default#vimrc-233}
match ErrorMsg '^\(<\|=\|>\)\{7\}\([^=].\+\)\?$'

" ----------------------------------------------------------------------------
" Syntax
" Needs to be in vimrc (or ftdetect) since syntax runs before ftplugin
" ----------------------------------------------------------------------------

" ----------------------------------------
" Filetype: markdown
" ----------------------------------------

" Variable to highlight markdown fenced code properly -- uses tpope's
" vim-markdown plugin (which is bundled with vim7.4 now)
" There are more syntaxes, but checking for them makes editing md very slow
let g:markdown_fenced_languages = [
      \ 'html',
      \ 'javascript', 'js=javascript', 'json=javascript',
      \ 'sass',
      \ ]

" ----------------------------------------
" Filetype: php
" ----------------------------------------

" $VIMRUNTIME/indent/php.vim and 2072/
let g:PHP_default_indenting = 0
let g:PHP_outdentphpescape  = 0

" ----------------------------------------
" Filetype: python
" ----------------------------------------

" $VIMRUNTIME/syntax/python.vim
let g:python_highlight_all = 1

" ----------------------------------------
" Filetype: sh
" ----------------------------------------

" $VIMRUNTIME/syntax/sh.vim - always assume bash
let g:is_bash = 1

" ----------------------------------------
" Filetype: vim
" ----------------------------------------

" $VIMRUNTIME/syntax/vim.vim
" disable mzscheme, tcl highlighting
let g:vimsyn_embed = 'lpPr'

" ============================================================================
" Multi-file projects
" This has to run super early
" ============================================================================

augroup dkoproject
  autocmd!

  autocmd BufNewFile,BufRead,BufWritePost
        \ *
        \ call dkoproject#GetProjectRoot()
augroup END

" ============================================================================
" Plugins
" ============================================================================

" ----------------------------------------------------------------------------
" Plugins: Disable distributed plugins
" To re-enable you have to comment them out (checks if defined, not if truthy)
" ----------------------------------------------------------------------------

let g:loaded_2html_plugin = 1
let g:loaded_getscriptPlugin = 1
let g:loaded_gzip = 1
let g:loaded_LogiPat = 1
let g:loaded_tarPlugin = 1
let g:loaded_zipPlugin = 1
" used to download spellfile and enable gx mapping
"let g:loaded_netrwPlugin = 0

" ----------------------------------------------------------------------------
" Plugins: netrw
" ----------------------------------------------------------------------------

" netrw in details format when no vimfiler
let g:netrw_liststyle = 3
let g:netrw_home = expand(dko#vim_dir . '/.tmp/cache')
let g:netrw_browsex_viewer = has('mac') ? 'open' : 'xdg-open'

" ----------------------------------------------------------------------------
" Plugins: load - called when vim-plug available
" ----------------------------------------------------------------------------

" This is used by plugin/completion.vim so is a g:
let g:dko_has_completion = 0

" @see https://github.com/junegunn/vim-plug/wiki/faq#conditional-activation
" @param  {Boolean}     cond    activate only if this is true
" @param  {Dictionary}  [opts]  activate with these additional options
" @return {Dictionary}
function! PlugWhen(cond, ...)
  let opts = get(a:000, 0, {})
  return a:cond ? opts : extend(opts, { 'on': [], 'for': [] })
endfunction

" Callback for 'do:' after update plugins that use neovim remote protocol
function! DoRemote(arg)
  UpdateRemotePlugins
endfunction

function! s:LoadPlugins()
  " Notes on adding plugins:
  " - Absolutely do not use 'for' if the plugin provides an `ftdetect/`

  " ==========================================================================
  " Plug: Async processes
  " ==========================================================================

  Plug 'Shougo/vimproc.vim', { 'do': 'make' }

  " ========================================================================
  " Plug: Colorscheme
  " ========================================================================

  Plug 'altercation/vim-colors-solarized'
  Plug 'morhetz/gruvbox'

  " ========================================================================
  " Plug: File system
  " ========================================================================

  " creates dir if new file in new dir
  Plug 'dockyard/vim-easydir'

  Plug 'ludovicchabant/vim-gutentags'
  "Plug 'davidosomething/vim-gutentags', { 'branch': 'user-autocmd' }

  " ========================================================================
  " Plug: Commands
  " ========================================================================

  " the blame in vim-gita is hawt :3
  Plug 'lambdalisue/vim-gita', { 'on': ['Gita'] }

  " Mostly for :Scriptnames and zS
  Plug 'tpope/vim-scriptease'

  " gK to lookup
  Plug 'keith/investigate.vim'

  Plug 'Shougo/unite.vim'
        \| Plug 'Shougo/vimfiler.vim'
        \| Plug 'Shougo/unite-outline'
        \| Plug 'tsukkee/unite-tag'

  " unite most-recently-used files menu
  Plug 'chemzqm/redismru.vim', PlugWhen(
        \   executable('redis-server') && executable('npm'),
        \   { 'do': 'npm install' }
        \ )
  Plug 'Shougo/neomru.vim', PlugWhen(
        \   !executable('redis-server') \|\| !executable('npm')
        \ )

  Plug 'nathanaelkane/vim-indent-guides'

  Plug 'osyo-manga/vim-over'

  " Add file manip commands like Remove, Move, Rename, SudoWrite
  Plug 'tpope/vim-eunuch'

  " ========================================================================
  " Plug: Input, syntax, spacing
  " ========================================================================

  Plug 'sgur/vim-editorconfig'
  "Plug 'davidosomething/vim-editorconfig', { 'branch': 'lint-skip-comments' }

  " highlight matching html tag
  Plug 'gregsexton/MatchTag'

  " add gS to smart split lines like comma lists and html tags
  Plug 'AndrewRadev/splitjoin.vim'

  Plug 'neomake/neomake'

  Plug 'tyru/current-func-info.vim'
  "Plug 'davidosomething/current-func-info.vim', { 'branch': 'javascript' }
  "Plug '~/projects/current-func-info.vim'

  " ctags on-the-fly processing
  " @TODO integrate into tabline
  "Plug 'majutsushi/tagbar'
  " This is a nice function context plugin but too many false positives
  "Plug 'jeetsukumaran/vim-gazetteer'

  " ========================================================================
  " Plug: Completion
  " ========================================================================

  Plug 'Shougo/neocomplete.vim', PlugWhen(!has('nvim') && has('lua'))
  Plug 'Shougo/deoplete.nvim', PlugWhen(
        \   has('nvim') && has('python3'),
        \   { 'do': function('DoRemote') }
        \ )

  let g:dko_has_completion =
        \ exists("g:plugs['neocomplete.vim']")
        \ || exists("g:plugs['deoplete.nvim']")

  if g:dko_has_completion
    "Full syntax completion - super slow, like syntaxcomplete
    "Plug 'Shougo/neco-syntax', { 'for': ['scss'] }

    " Mixed-filetype completion for Shougo complete, e.g. highlight JS within
    " Markdown fenced code blocks.
    Plug 'Shougo/context_filetype.vim'

    " Include completion
    Plug 'Shougo/neoinclude.vim'

    Plug 'Shougo/neosnippet'
          \| Plug 'honza/vim-snippets'
          \| Plug 'Shougo/neosnippet-snippets'

    Plug 'zchee/deoplete-jedi', PlugWhen(has('python3'))
  endif

  " ========================================================================
  " Plug: Editing keys
  " ========================================================================

  Plug 'godlygeek/tabular', { 'on': ['Tabularize'] }

  " custom C-x C-a mappings, see plugin/plug-increment-activator.vim
  Plug 'nishigori/increment-activator'

  Plug 'tomtom/tcomment_vim'

  " easyclip requires repeat, REMAPS all register keys (y,d,s,c,etc.)
  " and use 'm' instead
  Plug 'tpope/vim-repeat'
        \| Plug 'svermeulen/vim-easyclip'

  " used for line bubbling commands on mac
  " buffer switch, lnext/prev, etc.
  Plug 'tpope/vim-unimpaired'

  " REMAPS 's' to surround operator
  Plug 'kana/vim-operator-user'
        \| Plug 'rhysd/vim-operator-surround'
        \| Plug 'tyru/operator-camelize.vim'

  " e for entire file
  " i for indent level
  " l for current line
  " p for <?php ?>
  " u for url
  " b for any block type (parens, braces, quotes, ltgt)
  " = and i= for conflicts marked blocks
  " f for function (c, java, js, php, vim only)
  Plug 'kana/vim-textobj-user'
        \| Plug 'kana/vim-textobj-entire', { 'on': '<Plug>(textobj-entire' }
        \| Plug 'kana/vim-textobj-indent', { 'on': '<Plug>(textobj-indent' }
        \| Plug 'kana/vim-textobj-line', { 'on': '<Plug>(textobj-line' }
        \| Plug 'akiyan/vim-textobj-php'
        \| Plug 'mattn/vim-textobj-url'
        \| Plug 'rhysd/vim-textobj-anyblock', { 'on': '<Plug>(textobj-anyblock' }
        \| Plug 'rhysd/vim-textobj-conflict'
        \| Plug 'kana/vim-textobj-function'
        \| Plug 'thinca/vim-textobj-function-javascript'
        \| Plug 'kentaro/vim-textobj-function-php'

  Plug 'vim-scripts/matchit.zip'

  " ========================================================================
  " Plug: Language
  " ========================================================================

  " special end syntax for various langs
  Plug 'tpope/vim-endwise'

  " ========================================================================
  " Language: D
  " ========================================================================

  Plug 'idanarye/vim-dutyl', { 'for': 'd' }

  " ========================================================================
  " Language: Git
  " ========================================================================

  " creates gitconfig, gitcommit, rebase
  " provides :DiffGitCached in gitcommit file type
  " vim 7.4-77 ships with 2013 version, this is newer
  Plug 'tpope/vim-git'

  " show diff in own split when editing a COMMIT_EDITMSG
  Plug 'rhysd/committia.vim'

  " ========================================================================
  " Language: HTML, XML, and generators: mustache, handlebars
  " ========================================================================

  Plug 'othree/html5.vim'

  "Plug 'tpope/vim-haml'

  " Creates html.handlebars and other fts and sets syn
  Plug 'mustache/vim-mustache-handlebars'

  " ========================================================================
  " Language: JavaScript / CoffeeScript / JSON
  " ========================================================================

  Plug 'elzr/vim-json'

  " provides coffee ft
  Plug 'kchmck/vim-coffee-script'

  " TypeScript
  "Plug 'leafgarland/typescript-vim'

  " react/JSX syn highlighting for .cjsx
  " If you're using coffee+react instead of es6+react, quit your job.
  "Plug 'mtscout6/vim-cjsx'

  " ----------------------------------------
  " Syntax
  " ----------------------------------------

  " Options
  " 'jelera/vim-javascript-syntax' " Jelera: No indent file
  " 'pangloss/vim-javascript'      " pangloss' is probably the oldest, lot of
                                   " contributors but not up-to-date
                                   " Includes indent settings
  " 'othree/yajs.vim'              " othree has the latest support
  "                                " no indent file, fork of jelera
  " The "for" is required so the syntax registers on filetype, otherwise
  " yajs has trouble overriding the default js syntax due to runtime order
  Plug 'othree/yajs.vim', { 'for': 'javascript' }

  " ----------------------------------------
  " Syntax Addons
  " ----------------------------------------

  " Options
  "'gavocanov/vim-js-indent'                " Indent from pangloss, works well
                                            " with yajs well
  "'jiangmiao/simple-javascript-indenter'   " Alternative js indent
  "'jason0x43/vim-js-indent'                " Use HTML's indenter with
                                            " TypeScript support
  Plug 'gavocanov/vim-js-indent', { 'for': 'javascript' }

  " es.next support
  " possible jsx indent conflict?
  " @see https://github.com/othree/es.next.syntax.vim/issues/5
  "Plug 'othree/es.next.syntax.vim'

  " extends syntax for with jQuery,backbone,etc.
  Plug 'othree/javascript-libraries-syntax.vim'

  " After syntax, ftplugin, indent for JSX
  Plug 'mxw/vim-jsx'

  " ----------------------------------------
  " Features
  " ----------------------------------------

  " <leader>pd on function to insert jsdoc above
  Plug 'heavenshell/vim-jsdoc'
  "Plug 'davidosomething/vim-jsdoc'

  " detect extensionless node scripts (executables) via shebang
  " and add gf for going to node_modules files
  Plug 'moll/vim-node'

  " ----------------------------------------
  " Completion
  " ----------------------------------------

  " Parameter completion
  Plug 'othree/jspc.vim'

  if g:dko_has_completion
    Plug 'carlitux/deoplete-ternjs', PlugWhen(
          \   has('nvim') && executable('npm')
          \ )
    Plug 'marijnh/tern_for_vim', PlugWhen(
          \   !has('nvim') && executable('npm'),
          \   { 'do': 'npm install' }
          \ )
    Plug '1995eaton/vim-better-javascript-completion', PlugWhen(
          \   !executable('npm')
          \ )
  endif

  " ========================================================================
  " Language: Markdown
  " ========================================================================

  " Override vim included markdown ft* and syntax
  " The git repo has a newer syntax file than the one that ships with vim
  "Plug 'tpope/vim-markdown'

  " Use pandoc for markdown syntax
  Plug 'vim-pandoc/vim-pandoc-syntax'

  Plug 'suan/vim-instant-markdown', PlugWhen(
        \   executable('npm'),
        \   { 'do': 'npm install -g instant-markdown-d' }
        \ )

  " ========================================================================
  " Language: Nginx
  " ========================================================================

  Plug 'moskytw/nginx-contrib-vim'

  " ========================================================================
  " Language: PHP, twig
  " ========================================================================

  " ----------------------------------------
  " Syntax
  " ----------------------------------------

  " creates twig ft
  "Plug 'evidens/vim-twig'

  " Syntax
  Plug 'StanAngeloff/php.vim', { 'for': 'php' }

  " Indent
  " Fix indent of HTML in all PHP files -- basically adds
  " indent/html.vim when outside of PHP block.
  Plug '2072/PHP-Indenting-for-VIm'
        \| Plug 'captbaritone/better-indent-support-for-php-with-html'

  " ----------------------------------------
  " Features
  " ----------------------------------------

  " Completion
  " This is the upstream for the phpcomplete.vim in the $VIMRUNTIME

  " 'm2mdas/phpcomplete-extended' - fast via vimproc
  " 'mkusher/padawan.php'         - server based completion
  " 'phpvim/phpcd.vim'            - server based completion
  " 'shawncplus/phpcomplete.vim'  - slow as fuck, included in VIMRUNTIME.
  "                                 this is upstream. Manual <c-x><c-o> to use
  Plug 'shawncplus/phpcomplete.vim', { 'for': 'php' }
  "Plug 'm2mdas/phpcomplete-extended', { 'for': 'php' }
  "Plug 'dsawardekar/wordpress.vim', { 'for': ['php'] }

  " server-based PHP completion disabled
  Plug 'phpvim/phpcd.vim', PlugWhen(
        \   has('nvim') && executable('composer'),
        \   {
        \     'do': 'composer update',
        \     'for': 'php',
        \   }
        \ )
        \| Plug 'vim-scripts/progressbar-widget'
  Plug 'mkusher/padawan.vim', PlugWhen(
        \   has('nvim') && executable('composer') && has('python3'),
        \   {
        \     'do': 'composer global require mkusher/padawan',
        \     'for': 'php'
        \   }
        \ )

  " Snippet
  " Errors if no function under cursor,
  " Use 'doc' neosnippet instead
  " @see https://github.com/Shougo/neosnippet-snippets/blob/master/neosnippets/php.snip
  " Plug 'tobyS/vmustache', { 'for': ['php', 'blade'] }
  "       \| Plug 'tobyS/pdv', { 'for': ['php', 'blade'] }

  " ========================================================================
  " Language: Ruby, rails, puppet
  " ========================================================================

  " creates pp filetype
  "Plug 'rodjek/vim-puppet'

  " highlighting for Gemfile
  "Plug 'tpope/vim-bundler'

  " creates ruby filetype
  Plug 'vim-ruby/vim-ruby'

  " ========================================================================
  " Language: Stylesheets
  " ========================================================================

  " ----------------------------------------
  " Syntax
  " ----------------------------------------

  " creates less filetype
  "Plug 'groenewege/vim-less'

  " runtime css.vim provides @media syntax highlighting where hail2u doesn't
  " JulesWang/css.vim is the active repo for the css.vim bundled with vim
  " hail2u extends vim's css highlighting
  Plug 'JulesWang/css.vim'
        \| Plug 'hail2u/vim-css3-syntax'
        \| Plug 'cakebaker/scss-syntax.vim'

  " ----------------------------------------
  " Features
  " ----------------------------------------

  " Hex (et al) color highlighting
  Plug 'ap/vim-css-color'
  "Plug 'Rykka/colorv.vim', PlugWhen(0 && (has('python') || has('python3')))

  " Omnicompletion
  Plug 'othree/csscomplete.vim'

  " ========================================================================
  " Language: .tmux.conf
  " ========================================================================

  Plug 'tmux-plugins/vim-tmux'

  " ========================================================================
  " Language: VimL
  " ========================================================================

  " gf to go to where autoloaded function is defined
  Plug 'kana/vim-gf-user', { 'for': ['vim'] }
        \| Plug 'sgur/vim-gf-autoload', { 'for': ['vim'] }

  " Completion
  Plug 'Shougo/neco-vim', { 'for': ['vim'] }

  " ========================================================================
  " Plug: UI -- load last!
  " ========================================================================

  " :GitGutterToggle
  Plug 'airblade/vim-gitgutter', { 'on': ['GitGutterToggle'] }

  " <Plug> to not move on * search function
  Plug 'haya14busa/vim-asterisk'

  " Disabled on neovim
  " @see https://github.com/haya14busa/incsearch.vim/issues/79
  " highlight partial matches as you type in search mode
  Plug 'haya14busa/incsearch.vim', PlugWhen(!has('nvim'))

  " show registers in split if use \" or <C-R>
  "Plug 'junegunn/vim-peekaboo'

  " Show (#/total results) when searching for a term
  " disabled for echo issue
  " @see https://github.com/osyo-manga/vim-anzu/issues/19
  "Plug 'osyo-manga/vim-anzu'

  Plug 'now/vim-quit-if-only-quickfix-buffer-left'

  Plug 'tmux-plugins/vim-tmux-focus-events'
endfunction

" ----------------------------------------------------------------------------
" Plugins: autoinstall vim-plug, define plugins, install plugins if needed
" ----------------------------------------------------------------------------

let s:plug_dir = '/vendor/vim'
if has('nvim') | let s:plug_dir = '/vendor/nvim' | endif

" Load vim-plug and its plugins?
if empty(glob(expand(dko#vim_dir . '/autoload/plug.vim')))
  execute 'silent !curl -fLo '
        \ . dko#vim_dir . '/autoload/plug.vim '
        \ . 'https://raw.githubusercontent.com/'
        \ . 'junegunn/vim-plug/master/plug.vim'
endif

call plug#begin(dko#vim_dir . s:plug_dir)
  call s:LoadPlugins()
  if empty(glob(expand(g:plug_home))) | PlugInstall
  endif
call plug#end()

" ============================================================================
" Autocommands
" ============================================================================

" Window aucmds -- affect layout
augroup dkowindow
  autocmd!

  " Resize splits when the window is resized
  autocmd VimResized
        \ *
        \ wincmd =
augroup END

" Tabline
augroup dkotabline
  autocmd!

  autocmd vimrc VimEnter
        \ *
        \ set tabline=%!dkotabline#Output()

  autocmd BufWinEnter,Colorscheme,CursorMoved,Filetype,SessionLoadPost,WinEnter
        \ *
        \ call dkotabline#Refresh()
augroup END

" Statusline
" Must init statusline after plugs installed since it uses some plugins
" @TODO decouple
augroup dkostatusline
  autocmd!

  autocmd vimrc VimEnter
        \ *
        \ set statusline=%!dkostatus#Output(1)

  " initial statusline in case you type before VimEnter
  autocmd BufWinEnter,Colorscheme,Filetype,SessionLoadPost,WinEnter
        \ *
        \ call dkostatus#Refresh()
augroup END

" WordPress VIP specific
augroup dkowpvip
  autocmd!

  " No EOL character on files -- specifically for WP VIP PHP
  autocmd BufNewFile,BufRead
        \ */wp-content/themes/vip/*
        \ setlocal noeol binary fileformat=dos
augroup END

" ============================================================================
" Local vimrc
" ============================================================================

" Loads vimrc.vim from the ~/.secret/vim runtimepath (and wherever else it
" exists).
" The filename vimrc.vim only exists in ~/.secret/vim AFAIK, but might have to
" change it if I find out otherwise...
set runtimepath+=$HOME/.secret/vim
runtime! vimrc.vim

" ============================================================================
" Security
" ============================================================================

" Disallow unsafe local vimrc commands
" Leave down here since it trims local settings
set secure

